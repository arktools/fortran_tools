      SUBROUTINE ATMOS (A3,A8,A4)
!
!     THIS ROUTINE CALCULATES ATMOSPHERIC PROPERTIES OF THE
!     US STANDARD ATMOSPHERE,1962,ASSUMING AN INVERSE SQUARE
!     GRAVITATIONAL FIELD. THIS ASSUMPTION YIELDS DATA THAT
!     AGREES WITH THE COESA DOCUMENT WITHIN 1 PER CENT AT
!     ALL ALTITUDES UP TO 700 KILOMETERS (2296588 FEET). THE
!     DATA IS ARRANGED IN THE ATMOSPHERE ARRAY, A, AS
!     FOLLOWS
!     A(1) = CS, SPEED OF SOUND, FT/SEC
!     A(2) = (1/CS)(DCS/DZ), SOUND DERIVATIVE, 1/FT
!     A(3) = Z, GEOMETRIC ALTITUDE, FT
!     A(4) = P, PRESSURE, LB/FT2
!     A(5) = DP/DZ, PRESSURE DERIVATIVE, LB/FT3
!     A(6) = RHO, DENSITY, SLUGS/FT3
!     A(7) = (1/RHO)(DRHO/DZ), DENSITY DERIVATIVE, 1/FT
!     A(8) = T, TEMPERATURE, DEG RANKINE
!     A(9) = DT/DZ, TEMPERATURE DERIVATIVE, DEG RANKINE/FT
!
!     VARIOUS CONSTANTS USED
!      EARTH RADIUS                = 20890855 FT
!      SPECIFIC HEAT RATIO FOR AIR = 1.4
!     SEA LEVEL VALUES
!      GRAVITATIONAL ACCELERATION  = 32.1740484 FT/SEC2
!      MOLECULAR WEIGHT            = 28.9644
!      GO*MO/R*                    = 0.018743418 DEG RANK/FT
!
      DIMENSION  A( 9),HG(10),ZM(14),WM(14),TM(23),PM(22)
!
!     SET ARRAYS AND CONSTANT VALUES
      DATA G0,WM0,R0,GMRS/32.1740484,28.9644,20890855.0,&
     &     0.018743418/,HG/-16404.,0.0&
     &     ,36089.,65617.,104987.,154199.,170604.,200131.,&
     &     250186.,291160./,ZM/295276.,328084.,&
     &     360892.,393701.,492126.,524934.,557743.,623360.,&
     &     754593.,984252.,1312336.,1640420.,1968504.,&
     &     2296588./,WM/28.9644,28.88,28.56,&
     &     28.07,26.92,26.66,26.4,25.85,24.7,22.66,19.94,&
     &     17.94,16.84,16.17/
!
      DATA TM/577.17,518.67,389.97,389.97,411.57&
     &     ,487.17,487.17,454.77,325.17,325.17,379.17,469.17&
     &     ,649.17,1729.17,1999.17,2179.17,2431.17,2791.17&
     &     ,3295.17,3889.17,4357.17,4663.17,4861.17/,PM/&
     &     3711.0839,2116.2165,472.67563,114.34314,&
     &     18.128355,2.3162178,1.2321972,3.8030279E-01,&
     &     2.1671352E-02,3.4313478E-03,6.2773411E-04,1.53490&
          &91E-04,5.2624212E-05,1.0561806E-05,7.7083076E-06,&
     &     5.8267151E-06,3.5159854E-06,1.4520255E-06,3.92905&
          &63E-07,8.4030242E-08,2.2835256E-08,7.1875452E-09/
!
      A(3) = A3
!
!     CALCULATE G, Z, AND CHECK
!
 1000 Z = A(3)
      G = G0*(R0/(R0+Z))**2
      IF (Z .GT. 295276.0) GO TO 1040
!
!     TMS LINEAR WITH GEOPOTENTIAL. CALCULATE H AND SEARCH
!
      H     = R0*Z/(R0+Z)
      DO 1010 I= 2,10
         J     = I - 1
         IF (HG(I) .GE. H) GO TO 1020
 1010 CONTINUE
!
!     CALCULATE TMS SLOPE,TMS, AND SET MOL WT STUFF
!
 1020 ELH   = (TM(J+1) - TM(J))/(HG(J+1) - HG(J))
      TMS   = TM(J) + ELH*(H - HG(J))
      ELZ   = ELH*G/G0
      DMDZ  = 0.0
      EM    = WM0
!
!     CHECK TMS SLOPE AND CALCULATE PRESSURE
!
      IF (ELH .EQ. 0.0) GO TO 1030
!
!     NON - ZERO SLOPE PRESSURE EQUATION
!
      A(4) = PM(J)*(TM(J)/TMS)**(GMRS/ELH)
      GO TO 1070
!
!     ZERO SLOPE PRESSURE EQUATION
!
 1030 A(4) = PM(J)*EXP(GMRS*(HG(J)-H)/TMS)
      GO TO 1070
!
!     TMS LINEAR WITH Z. SEARCH MATRIX
!
 1040 DO 1050 I = 2,14
         J      = I + 8
         K      = I - 1
         IF (ZM(I) .GE. Z) GO TO 1060
 1050 CONTINUE
!
!     CALCULATE TMS, SLOPE, AND STUFF
!
 1060 ELZ  = (TM(J+1) - TM(J))/(ZM(K+1) - ZM(K))
      TMS  = TM(J) + ELZ*(Z - ZM(K))
      DMDZ = (WM(K+1) - WM(K))/(ZM(K+1) - ZM(K))
      EM = WM(K) + DMDZ*(Z - ZM(K))
      ZLZ  = Z - TMS/ELZ
!
!     PRESSURE EQUATION FOR TMS LINEAR WITH Z
!
      A(4) = PM(J)*EXP(GMRS/ELZ*(R0/(R0+ZLZ))**2*((Z-ZM(K))*&
     &       (R0+ZLZ)/(R0+Z)/(R0+ZM(K)) - ALOG(TMS*(R0+ZM(K)&
     &       )/TM(J)/(R0+Z))))
!
!     CALCULATE SOUND SPEED AND DERIVATIVE
!
 1070 A(1) = 49.022164*SQRT(TMS)
      A(2) = 0.5*ELZ/TMS
!
!     CALCULATE DENSITY, DERIVATIVE, AND PRESSURE DERIVATIVE
!
      A(6) = GMRS*A(4)/G0/TMS
      A(7) = - (A(6)*G/A(4) + ELZ/TMS)
      A(5) = -A(6)*G
!
!     CALCULATE TEMPERATURE, DERIVATIVE, AND LEAVE
!
      A(8) = EM*TMS/WM0
      A(9) = (EM*ELZ + TMS*DMDZ)/WM0
      A8 = A(8)
      A4 = A(4)
      A1 = A(1)
      A6 = A(6)
      RETURN
      END
